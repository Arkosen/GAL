#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long;

#-----------------------------------------------------------------------------
#----------------------------------- MAIN ------------------------------------
#-----------------------------------------------------------------------------
my $usage = "

Synopsis:

gvf2annovar_input variants.gvf

Description:

This script will convert GVF files to an input format appropriate for
Annovar's annotate_variants.pl.

Columns:

seqid            GFF3 sequence ID
start		 Starting position (1-based) on seqid
end		 Ending position (1-based) on seqid
reference_seq	 Reference sequence
variant_seq      Non-reference variant sequence (one per line).
line count	 The line number this variant came from in the
                 original GFF3 file (allows the original GFF3 file
                 to be regenerated)
source		 GFF3 source field
type		 GFF3 type field
score		 GFF3 score field
strand		 GFF3 strand field
phase		 GFF3 phase field
attributes       GFF3 attributes

";


my ($help);
my $opt_success = GetOptions('help'    => \$help,
			    );

if (! $opt_success) {
    print STDERR join ' : ', ('FATAL',
                              'command_line_parse_error',
                              'Use gff_tool --help to see correct usage');
}

if ($help) {
 print $usage;
 exit(0);
}

my $file = shift;
die $usage unless $file;
open (my $IN, '<', $file) or die "Can't open $file for reading\n$!\n";

my $line_count = 0;
while (<$IN>) {

  next if /^\s*$/;
  next if /^\s*\#/;
  chomp;
  my ($seqid, $source, $type, $start, $end, $score, $strand, $phase,
      $attribute_text) = split /\t/, $_;

  my ($variant_text) = $attribute_text =~ /Variant_seq=(.*?);/;
  my @variant_seqs = split /,/, $variant_text;

  my ($reference_seq) = $attribute_text =~ /Reference_seq=(.*?);/;

  @variant_seqs = grep {$_ ne $reference_seq} @variant_seqs;

  my ($feature_id) = $attribute_text =~ /ID=(.*?);/;

  for my $this_variant_seq (@variant_seqs) {

    print join "\t", ($seqid,
		      $start,
		      $end,
		      $reference_seq,
		      $this_variant_seq,
		      $feature_id,
		      $source,
		      $type,
		      $score,
		      $strand,
		      $phase,
		      $attribute_text,
		     );
    print "\n";
  }
}

#-----------------------------------------------------------------------------
#-------------------------------- SUBROUTINES --------------------------------
#-----------------------------------------------------------------------------
