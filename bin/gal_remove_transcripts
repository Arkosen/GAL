#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long;

use FindBin;
use lib "$FindBin::RealBin/../lib";

use GAL::Annotation;

#-----------------------------------------------------------------------------
#----------------------------------- MAIN ------------------------------------
#-----------------------------------------------------------------------------

my $usage = "

Synopsis:

gal_remove_transcripts feature.gff3

Description:

This script will remove a set of transcripts from a GFF3 dataset based
on a input list of IDs.

";

my ($help, $ids);

my $opt_success = GetOptions('help|h'  => \$help,
			     'ids|i=s' => \$ids,
			      );

die $usage unless $opt_success;
if ($help) {
  print $usage;
  exit(0);
}


my ($gff3_file) = @ARGV;

die "$usage\n\nFATAL : missing_gff3_file : GFF3 file required\n" unless $gff3_file;

my $annotation = GAL::Annotation->new($gff3_file);

my $features = $annotation->features;

my @ids;
if (-r $ids) {
  open(my $IN, '<', $ids) or die "FATAL : cant_open_id_file : $ids\n";
  @ids = (<$IN>);
}
else {
  @ids = split /,/, $ids;
}
chomp @ids;
my %ids = map {$_ => 1} @ids;

if ($gff3_file =~ /\.gff3$/) {
  my $meta_data = `grep -P '^\#' $gff3_file`;
  chomp $meta_data;
  print $meta_data;
  print "\n";
}

# Do the search and get an interator for all matching features
my $genes = $features->search({type => 'gene'});

my $id_count = scalar @ids;
print "# Transcripts whose IDs were found in the ID file $ids ($id_count total IDs) were removed by GAL/bin/examples/gal_remove_transcripts " . `date`;

my $skip_count;
# Iterate over the genes;
GENE:
while (my $gene = $genes->next) {
  my @transcripts = $gene->transcripts;
  my @keep;
  while (my $transcript = shift @transcripts) {
    if (exists $ids{$transcript->feature_id}) {
      $annotation->info('skipping_transcript', $transcript->feature_id);
      $skip_count++;
    }
    else {
      push @keep, $transcript;
    }
  }
  next GENE unless scalar @keep;
  print $gene->to_gff3;
  print "\n";
  for my $transcript (@keep) {
    my @lines = $transcript->to_gff3_recursive;
    print join "\n", @lines;
    print "\n";
  }
}
$annotation->info('total_transcript_skipped', $skip_count);
