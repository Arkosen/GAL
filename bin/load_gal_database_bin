#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long;
use IO::Prompt;
use DBI;
use File::Temp qw(tempfile);

#-----------------------------------------------------------------------------
#----------------------------------- MAIN ------------------------------------
#-----------------------------------------------------------------------------
my $usage = "

Synopsis:

load_gal_database --create --format gff3 --dsn 'dbi:mysql:gff_db' \
                  file.gff3 [another_file.gff3]

Description:

Load a feature file into a GAL database.

Options:

  create     Drop (if exists) and create the database first.  Default is
             to add to  the existing database if it exists.
  format     The format of the input file (a subclass of GAL::Parser).
  dsn        The DBI connection string (e.g. dbi:mysql:database).
	     Currently only expected to work with MySQL.
  user       The database user.  Defaults first to \$ENV{\${driver}_user}
             where \$driver is the driver parsed from the dsn. ['']
  password   The database password.   Defaults first to
	     \$ENV{\${driver}_password} where \$driver is the driver
	     parsed from the dsn.
  prompt     Prompt for the password on the command line.
  temp_dir   A directory for temporary files.  If none is given, perl
             will try to choose something logical.

";


my ($help, $create, $format, $dsn, $user, $password, $prompt, $temp_dir);
my $opt_success = GetOptions('help'         => \$help,
                             'dsn=s'        => \$dsn,
                             'format=s'     => \$format,
  			     'create'       => \$create,
 			     'user=s'       => \$user,
 			     'password=s'   => \$password,
 			     'prompt'       => sub {$password = prompt("Password: ", -tty,  -echo => '*')->{value}},
			     'temp_dir=s'   => \$temp_dir,
);

die $usage if $help || ! $opt_success;

my @files = @ARGV;
die $usage unless scalar @files;
die $usage if grep {! -r $_}  @files;

my ($scheme, $driver) = split /:/, $dsn;

$user     ||= $ENV{"${driver}_user"};
$password ||= $ENV{"${driver}_password"};


my $class = "GAL::Parser::$format";
eval "require $class";
die $@ if $@;

($temp_dir) = grep {-d $_} qw(/tmp .) unless ($temp_dir &&-d $temp_dir);

my ($FEAT_TEMP,  $feat_filename)  = tempfile('gal_feat_XXXXXX',
                                           SUFFIX => '.tmp',
                                           DIR    => $temp_dir,
                                           UNLINK => 0,
                                           );

my ($ATT_TEMP,  $att_filename)  = tempfile('gal_att_XXXXXX',
                                           SUFFIX => '.tmp',
                                           DIR    => $temp_dir,
                                           UNLINK => 0,
                                           );
chmod (0444, $feat_filename, $att_filename);

for my $file (@files) {
	        
	my $parser = $class->new(file => $file);


	while (my $feature = $parser->next_feature_hash) {
	        my $feature_id = $feature->{feature_id};
	        my $bins = bin_feature($feature);
	        my $bin = $bins->[0];
	        my @feature_data = (@{$feature}{qw(feature_id seqid source
                                                   type start end score strand
                                                   phase)},
                                    $bin);
	        my $attributes = $feature->{attributes};
	        print $FEAT_TEMP join "\t", @feature_data;
	        print $FEAT_TEMP "\n";

	        for my $key (keys %{$attributes}) {
	                my @values = @{$attributes->{$key}};
	                for my $value (@values) {
	                        print $ATT_TEMP  join "\t",
                                    ($feature_id, $key, $value);
	                        print $ATT_TEMP "\n";
	                }
	        }
	}
}
create_db($dsn) if $create;

my $dbh = DBI->connect($dsn, $user, $password);

$dbh->do("LOAD DATA INFILE '$feat_filename' INTO TABLE feature   (feature_id, seqid, source, type, start, end, score, strand, phase, bin)");
$dbh->do("LOAD DATA INFILE '$att_filename'  INTO TABLE attribute (feature_id, att_key, att_value)");

if ($create) {
	$dbh->do("CREATE INDEX feature_id_index USING BTREE ON feature (feature_id)");
	$dbh->do("CREATE INDEX bin_index USING BTREE ON feature (bin)");
	$dbh->do("CREATE INDEX att_feature_id_index USING BTREE ON attribute (feature_id)");
	$dbh->do("CREATE INDEX att_key_value_index USING BTREE ON attribute (att_key, att_value)");
}

#-----------------------------------------------------------------------------
#-------------------------------- SUBROUTINES --------------------------------
#-----------------------------------------------------------------------------
sub create_db {

	my $dsn = shift;

	my ($db_name) = $dsn =~ /([^:]+)$/;

	my $attributes = {host     => 'localhost',
			  user     => $user,
			  password => $password,
			  };

	my $drh = DBI->install_driver('mysql', $attributes);

	if (grep {$_ eq "DBI:mysql:$db_name"} $drh->data_sources($attributes)) {
		$drh->func('dropdb'  , $db_name, 'localhost', $user, $password, 'admin') or die "Can't drop database $db_name\n";
		$drh->func('createdb', $db_name, 'localhost', $user, $password, 'admin') or die "Can't create database $db_name\n";
	}
	else {
		$drh->func('createdb', $db_name, 'localhost', $user, $password, 'admin') or die "Can't create database $db_name\n";
	}

	my $dbh = DBI->connect($dsn, $user, $password);

	$dbh->do("DROP TABLE IF EXISTS feature");
	$dbh->do("DROP TABLE IF EXISTS attribute");
	$dbh->do("CREATE TABLE feature ("    .
		 "feature_id VARCHAR(100), " .
		 "seqid      VARCHAR(100), " .
		 "source     VARCHAR(100), " .
		 "type       VARCHAR(100), " .
		 "start      INT, "          .
		 "end        INT, "          .
		 "score      VARCHAR(20), "  .
		 "strand     VARCHAR(1), "   .
		 "phase      VARCHAR(1),"     .
		 "bin        VARCHAR(15))"
		 # "name VARCHAR(100), "      .
		 # "parent VARCHAR(100), "    .
		 );
	$dbh->do("CREATE TABLE attribute ("  .
		 "attribute_id INT NOT NULL AUTO_INCREMENT, " .
		 "feature_id VARCHAR(100), " .
		 "att_key VARCHAR(100), "    .
		 "att_value VARCHAR(255), "  .
		 "PRIMARY KEY (attribute_id))"
		 );
}

#-----------------------------------------------------------------------------

sub bin_feature {

    my $feature = shift;

    my ($seqid, $start, $end) = @{$feature}{qw(seqid start end)};

    my @feature_bins;
    my $count;
    my $single_bin;
    for my $bin_size (128_000, 1_000_000, 8_000_000, 64_000_000, 
		      512_000_000) {
	$count++;
	my $start_bin = int($start/$bin_size);
	my $end_bin   = int($end/$bin_size);
	my @these_bins = map {$_ = join ':', ($seqid, $count, $_)} ($start_bin .. $end_bin);
	if (! $single_bin && scalar @these_bins == 1) {
	    $single_bin = shift @these_bins;
	}
	unshift @feature_bins, @these_bins;
    }
    unshift @feature_bins, $single_bin;
    return wantarray ? @feature_bins : \@feature_bins;
}
